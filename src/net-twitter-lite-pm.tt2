[% VERSION = '0.00000_01' -%]
package Net::Twitter::Lite;
use 5.008;
use Moose;
use Carp;
use JSON::Any qw/XS DWIW JSON/;
use URI::Escape;
use aliased 'Net::Twitter::Lite::API::REST' => 'API';

# use *all* digits for fBSD ports
our $VERSION = '[% VERSION %]';

$VERSION = eval $VERSION; # numify for warning-free dev releases

has useragent_class => ( isa => 'Str', is => 'ro', default => 'LWP::UserAgent' );
has username        => ( isa => 'Str', is => 'rw' );
has password        => ( isa => 'Str', is => 'rw' );
has useragent       => ( isa => 'Str', is => 'ro', default => __PACKAGE__ . "/$VERSION" );
has source          => ( isa => 'Str', is => 'ro', default => 'twitterpm' );
has apiurl          => ( isa => 'Str', is => 'ro', default => API->base_url );
has apihost         => ( isa => 'Str', is => 'ro', default => 'twitter.com:80' );
has apirealm        => ( isa => 'Str', is => 'ro', default => 'Twitter API' );
has _ua             => ( isa => 'Object', is => 'rw' );

use Exception::Class (
    TwitterException => {
        description => 'Twitter API error',
        fields      => [qw/http_response twitter_error/ ],
    },
    HttpException   => {
        description => 'HTTP or network error',
        fields      => [qw/http_response/],
    },
);

sub BUILD {
    my $self = shift;

    eval "use " . $self->useragent_class;
    croak $@ if $@;

    my $ua = $self->_ua($self->useragent_class->new);
    $ua->credentials($self->apihost, $self->apirealm, $self->username, $self->password)
        if $self->username;
}

sub credentials {
    my ($self, $username, $password) = @_;

    $self->username($username);
    $self->password($password);

    $self->_ua->credentials(@{$self}{qw/apihost apirealm username password/});

    return $self;
}

my $post_request = sub {
    my ($ua, $uri, $args) = @_;
    return $ua->post($uri, $args);
};

my $get_request = sub {
    my ($ua, $uri, $args) = @_;
    $uri->query_form($args);
    return $ua->get($uri);
};

my $with_url_arg = sub {
    my ($path, $args) = @_;

    if ( defined(my $id = delete $args->{id}) ) {
        $path .= uri_escape($id);
    }
    else {
        chop($path);
    }
    return $path;
};

my $method_defs = API->method_definitions;
while ( my ($method, $def) = each %$method_defs ) {
    my ($arg_names, $path) = @{$def}{qw/required path/};
    my $request = $def->{method} eq 'POST' ? $post_request : $get_request;

    my $modify_path = $path =~ s,/id$,/, ? $with_url_arg : sub { $_[0] };

    my $code = sub {
        my $self = shift;

        my $args = {};
        if ( ref $_[0] ) {
            UNIVERSAL::isa($_[0], 'HASH') && @_ == 1 || croak "$method expected a single HASH ref argument";
            $args = { %{shift()} }; # copy callers args since we may add ->{source}
        }
        elsif ( @_ ) {
            @_ == @$arg_names || croak "$method expected @{[ scalar @$arg_names ]} args";
            @{$args}{@$arg_names} = @_;
        }
        $args->{source} ||= $self->source if $method eq 'update';

        my $local_path = $modify_path->($path, $args);
        my $uri = URI->new($self->apiurl . "/$local_path.json");
        my $res = $self->_response($request->($self->_ua, $uri, $args));
        my $obj = eval { JSON::Any->from_json($res->content) };

        return $obj if $res->is_success && $obj;
        TwitterException->throw(
            error         => $obj->{error},
            http_response => $res,
            twitter_error => $obj
        ) if $obj;
        HttpException->throw(
            error         => $res->message,
            http_response => $res,
        );
    };

    __PACKAGE__->meta->add_method($_, $code) for ( $method, @{$def->{aliases} || []});
}

__PACKAGE__->meta->make_immutable;

1;

__END__

=head1 NAME

Net::Twitter::Lite - A lean perl interface to the Twitter API

=head1 VERSION

This document describes version [% VERSION %]

=head1 SYNOPSIS

  use Net::Twitter::Light;

  my $ntl = Net::Twitter::Light->new(username => $user, password => $password);

  my $result = $ntl->update('Hello, world!');

  eval {
      my $feed = $ntl->friends_timeline({ since_id => $high_water, count => 100 });
      for my $status ( @$feed ) {
          say "$status->{user}{screen_name}: $status->{text};
      }
  };
  if ( @_ ) {
      if( my $e = TwitterException->caught() ) {
          warn "$e\n";
      }
      elsif ( $e = HttpException->caught() ) {
          warn "Network connection down?\n";
      }
      else {
          die $@; # something bad happened
      }
  }


=head1 DESCRIPTION

C<Net::Twitter::Lite> attempts to provide a lean, robust, and easy to maintain
perl interface to the Twitter API. It provides some basic compatibility with
the L<Net::Twitter> module.  It takes a different approach in some areas,
however, so it is not a drop-in replacement.

Most Twitter API methods take parameters.  All C<Net::Twitter::Lite> methods
will accept a hash ref of named parameters as specified in the Twitter API
documentation.  For convenience, many C<Net::Twitter::Lite> methods accept
simple placeholder arguments as documented, below.  The placeholder parameter
passing style is optional; you can always use the named parameters in a hash
ref if you prefer.

C<Net::Twitter::Lite> does not do aggressive parameter validation. It will
dutifully pass invalid parameters to Twitter if instructed, and if Twitter
returns an error as a result, C<Net::Twitter::Lite> will throw an appropriate
exception.

The lack of aggressive parameter checking has an advantage.  If Twitter adds
new parameters to any of the API methods, you may begin using them immediately
without warning or errors and without any need for modification to the source
code of this module.

C<Net::Twitter::Lite> differs from C<Net::Twitter> significantly in its error
handling strategy.  It throws exceptions in response to Twitter and network
errors.  You can catch and deal with these errors using eval blocks in the
usual way.  Throwing exceptions allows C<Net::Twitter::Lite> to work well with
C<LWP::UserAgent::POE> in an environment where there may be concurrent
requests.

For compatibility with C<Net::Twitter>, C<Net::Twitter::Lite> provides a
C<compat_mode> option to C<new>.  In compatibility mode, C<Net::Twitter::Lite>
will return C<undef> on error.  Use the C<get_error>, C<http_code>, and
C<http_message> methods to determine the nature of the error.

=head1 METHODS

=over 4

=item new

=item credentials

=back

=head1 API METHODS

[% FOREACH section IN api_def;
   title = section.0; 
   methods = section.1  -%]
=head2 [% title %]

=over 4

[% FOREACH method IN methods;
   name = method.0;
   def  = method.1;
   NEXT IF def.deprecated -%]
=item B<[% name %]>

[% IF def.required.size -%]
=item B<[% name %]([% def.required.join(", ") %])>

[% END -%]

[% FOREACH alias IN def.aliases -%]
=item alias: [% alias %]

[% END -%]
Parameters: [% IF def.params.size %][% def.params.join(", ") %][% ELSE %]I<none>[% END %]

[% def.description %]

Returns: [% def.returns %]

[% END -%]
=back

[% END -%]


=head1 SEE ALSO

=over 4

=item L<http://apiwiki.twitter.com/REST+API+Documentation>

This is the official Twitter API documentation. It describes the methods and their
parameters in more detail and may be more current than the documentation provided
with this module.

=item L<Net::Twitter>

This is the original perl interface to the Twitter API and inspiration for this
module.

=back

=head1 AUTHOR

Marc Mims <marc@questright.com>

=head1 LICENSE

Copyright (c) 2009 Marc Mims

The Twitter API itself, and the description text used in this module is:

Copyright (c) 2009 Twitter

This library is free software; you can redistribute it and/or modify it under the same terms as Perl itself.
